<!DOCTYPE html><html lang="en"><head><meta charset="UTF-8" /><meta name="viewport" content="width=device-width, initial-scale=1.0" /><title></title></head>
<style>canvas{ display:block; } body, html { padding:0px; margin:0px; width:100%; height:100%; background-color:black; }</style>
<body><script src="../import-map.js"></script><script type="module">
// #region IMPORTS
import useThreeWebGL2, { THREE, useDarkScene } from '../lib/useThreeWebGL2.js';
import TexturePass      from '../lib/misc/TexturePass.js';
import TextureCanvas    from '../lib/misc/TextureCanvas.js'; 
import GridAlternative  from '../lib/meshes/GridAlternative.js';
// #endregion

// #region MAIN
let App   = useDarkScene( useThreeWebGL2() );
let Ref   = {};

window.addEventListener( 'load', async _=>{
    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    App.sphericalLook( 0, 30, 10 );

    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    const matHeight = await HeightmapMaterial();
    const texPass   = new TexturePass( matHeight, 256, 256 );
    texPass.render( App.renderer );

    const matDisplace = await DisplacementMaterial( texPass.texture );
    const mesh = GridAlternative.mesh( matDisplace, 4, 4, 150, 150, false, false );    
    App.scene.add( mesh );

    const quad = new THREE.Mesh( 
        new THREE.PlaneGeometry( 1, 1 ), 
        new THREE.MeshBasicMaterial( { map:texPass.texture, side:THREE.DoubleSide, } ) 
    );
    quad.position.set( -2, 1.0, 0 );
    quad.scale.setScalar( 2 );
    App.scene.add( quad );

    // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    App.renderLoop();
});
// #endregion


// https://discourse.threejs.org/t/calculating-vertex-normals-after-displacement-in-the-vertex-shader/16989/8
// https://github.com/jsulpis/3d-terrain-r3f/
// https://jsulpis.github.io/3d-terrain-r3f/

async function DisplacementMaterial( tex ){
    return new THREE.RawShaderMaterial({
    depthTest       : true,
    transparent 	: true,
	side			: THREE.DoubleSide,
    uniforms        : { 
        heightmap   : { type :'sampler2D', value:tex },
        hScale      : { type :'float', value: 1.5 },
    },
    
    vertexShader    : `#version 300 es
    in vec3 position;
    in vec3 normal;
    in vec2 uv;
    
    uniform mat4 modelMatrix;
    uniform mat4 viewMatrix;
    uniform mat4 projectionMatrix;

    uniform sampler2D heightmap;
    uniform mediump float hScale;

    out vec3 fragNorm;
    out vec3 fragWPos;
    out vec3 fragLPos;
    out vec2 fragUV;
    out float fragN;
   
    /////////////////////////////////////////////////////////////////

    // http://lolengine.net/blog/2013/09/21/picking-orthogonal-vector-combing-coconuts
    vec3 orthogonal(vec3 v) {
        return normalize(
            abs(v.x) > abs(v.z) ? 
                vec3( -v.y,  v.x, 0.0 ) : 
                vec3(  0.0, -v.z, v.y )
        );
    }

    // https://discourse.threejs.org/t/calculating-vertex-normals-after-displacement-in-the-vertex-shader/16989/8
    vec3 displacementNormal( vec3 dispPos, vec3 pos, vec3 norm, vec2 uv, float hScale, sampler2D map ){
        // HARD CODED VALUES, Should by dynamic
        float texelSize = 1.0 / 256.0;
        float offset    = 0.01;

        // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        // Get Neighboring Points using Tangent + Bit Tangent
        vec3 tangent    = orthogonal( norm );
        vec3 bitTangent = normalize( cross( norm, tangent ) );

        vec3 pos1       = pos + tangent * offset;
        vec3 pos2       = pos + bitTangent * offset;

        // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        // Grab the Displacement for the neighboring points
        vec2 uv1        = uv + vec2( -texelSize, 0.0 );
        vec2 uv2        = uv + vec2( 0.0, -texelSize );

        vec3 dispPos1   = pos1 + norm * ( texture( map, uv1 ).r * hScale );
        vec3 dispPos2   = pos2 + norm * ( texture( map, uv2 ).r * hScale );

        // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        // Compute the final displacement normal
        vec3 dispTan    = dispPos1 - dispPos;
        vec3 dispBTan   = dispPos2 - dispPos;

        return normalize( cross( dispTan, dispBTan ) );
    }

    /////////////////////////////////////////////////////////////////

    void main(){
        // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        // DISPLACEMENT
        vec3 pos     = position;
        vec4 px      = texture( heightmap, uv );
        pos.y        = px.x * hScale;

        fragNorm     = displacementNormal( pos, position, normal, uv, hScale, heightmap );

        pos.y        = max( 0.01, px.x - 0.42 ) * hScale;

        // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        vec4 wPos   = modelMatrix * vec4( pos, 1.0 );
        fragWPos    = wPos.xyz;
        fragLPos    = position;
        fragUV      = uv;
        fragN       = px.x;

        // ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        gl_Position = projectionMatrix * viewMatrix * wPos;
    }`,
	
    fragmentShader  : `#version 300 es
    precision mediump float;

    uniform sampler2D heightmap;
    uniform sampler2D linetex;
    uniform mediump float hScale;

    in vec3  fragNorm;
    in vec3  fragWPos;
    in vec3  fragLPos;
    in vec2  fragUV;
    in float fragN;

    out vec4  outColor;

    ////////////////////////////////////////////////////////////////////////
    vec3 rgb( int c ){
        return vec3(
            float( ( c >> 16 ) & 0xff ) * 0.00392156863,
            float( ( c >> 8 ) & 0xff ) * 0.00392156863,
            float( c & 0xff ) * 0.00392156863
        );
    }

    vec3 hsl2rgb( in vec3 c ){
        vec3 rgb = clamp( abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),6.0)-3.0)-1.0, 0.0, 1.0 );
        return c.z + c.y * (rgb-0.5)*(1.0-abs(2.0*c.z-1.0));
    }

    vec3 rgb2hsl( in vec3 c ){
        float h = 0.0;
        float s = 0.0;
        float l = 0.0;
        float r = c.r;
        float g = c.g;
        float b = c.b;
        float cMin = min( r, min( g, b ) );
        float cMax = max( r, max( g, b ) );

        l = ( cMax + cMin ) / 2.0;
        if( cMax > cMin ){
            float cDelta = cMax - cMin;
            
            //s = l < .05 ? cDelta / ( cMax + cMin ) : cDelta / ( 2.0 - ( cMax + cMin ) ); Original
            s = l < .0 ? cDelta / ( cMax + cMin ) : cDelta / ( 2.0 - ( cMax + cMin ) );
            
            if ( r == cMax )        h = ( g - b ) / cDelta;
            else if ( g == cMax )   h = 2.0 + ( b - r ) / cDelta;
            else                    h = 4.0 + ( r - g ) / cDelta;

            if( h < 0.0 ) h += 6.0;
            h = h / 6.0;
        }

        return vec3( h, s, l );
    }

    vec3 gamma( vec3 c ){
        return vec3(
            ( c.r <= 31308e-7 )? c.r * 12.92 : 1.055 * pow( c.r, 1.0 / 2.4) - 0.055,
            ( c.g <= 31308e-7 )? c.g * 12.92 : 1.055 * pow( c.g, 1.0 / 2.4) - 0.055,
            ( c.b <= 31308e-7 )? c.b * 12.92 : 1.055 * pow( c.b, 1.0 / 2.4) - 0.055
        );
    }

    float remap( float value, float min1, float max1, float min2, float max2 ){
        return min2 + (value - min1) * (max2 - min2) / (max1 - min1);
    }

    ////////////////////////////////////////////////////////////////////////

    void main(){
        vec3[] colors = vec3[](
            rgb( 0x00a9ff ),    // Water
            rgb( 0xffd68f ),    // Shore
            rgb( 0xefb28f ),    // Beach
            rgb( 0x9ea667 ),    // Shrub
            rgb( 0x586647 ),    // Forest
            rgb( 0x656565 ),    // Stone
            rgb( 0x9aa7ad )     // Snow
        );

        float[] heights = float[](
            0.42,
            0.01,
            0.04,
            0.1,
            0.29,
            0.36,
            0.6
        );

        vec3 color = vec3( fragWPos.y );

        if( fragN <= heights[ 0 ] ){
            color    = colors[ 0 ];
            vec3 hsl = rgb2hsl( color );

            hsl.y *= 1.7;

            hsl.z = clamp( pow( fragN / 0.42, 4.0 ), 0.0, 0.9 );

            // hsl.z *= remap(
            //     pow( 1.0 - ( 0.42 - fragN ) * 1.3, 6.0 ), // 0.42 is water line
            //     0.0, 1.0, 0.0, 1.9
            // );
            
            color = hsl2rgb( hsl );
        }else{
            for( int i=1; i < 7; i++ ){
                if( fragN <= (heights[0] + heights[i]) ){
                    color = colors[ i ];
                    break;
                }
            }

            vec3 hsl = rgb2hsl( color );
            hsl.y *= 1.7;
            color = hsl2rgb( hsl );
        }


        vec3 norm     = normalize( cross( dFdx( fragWPos), dFdy( fragWPos ) ) );
        //vec3 norm   = normalize( fragNorm );
        float diffuse = dot( norm, normalize( vec3( 0.5,0.5,0.5 ) ) );
        diffuse = max( 0.3, diffuse );

        outColor.rgb = color * diffuse;
        outColor.a = 1.0;

        // if( fragN >= 0.45 ) outColor.rgb = colors[2];
    }`
    });
}

async function HeightmapMaterial(){
    return new THREE.RawShaderMaterial({
    // depthTest       : true,
    // transparent 	: false,
	// side			: THREE.DoubleSide,
    uniforms        : { 
    },
    
    vertexShader    : `#version 300 es
    in vec3 position;
    in vec2 uv;
    
    uniform mat4 modelMatrix;
    uniform mat4 viewMatrix;
    uniform mat4 projectionMatrix;

    out vec3 fragWPos;
    out vec2 fragUV;

    void main(){
        vec4 wPos   = modelMatrix * vec4( position, 1.0 );;
        
        fragUV      = uv;
        fragWPos    = wPos.xyz;

        gl_Position = projectionMatrix * viewMatrix * wPos;
    }`,
	
    fragmentShader  : `#version 300 es
    precision mediump float;

    in vec3 fragWPos;
    in vec2 fragUV;
    out vec4  outColor;

    ////////////////////////////////////////////////////////////////////////
    // #region NOISE

    // From https://github.com/hughsk/glsl-noise/blob/master/periodic/2d.glsl

    //
    // GLSL textureless classic 2D noise "cnoise",
    // with an RSL-style periodic variant "pnoise".
    // Author:  Stefan Gustavson (stefan.gustavson@liu.se)
    // Version: 2011-08-22
    //
    // Many thanks to Ian McEwan of Ashima Arts for the
    // ideas for permutation and gradient selection.
    //
    // Copyright (c) 2011 Stefan Gustavson. All rights reserved.
    // Distributed under the MIT license. See LICENSE file.
    // https://github.com/ashima/webgl-noise
    //

    vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
    vec4 permute(vec4 x) { return mod289(((x * 34.0) + 1.0) * x); }
    vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
    vec2 fade(vec2 t) { return t * t * t * (t * (t * 6.0 - 15.0) + 10.0); }

    float map(float value, float min1, float max1, float min2, float max2) {
        return min2 + (value - min1) * (max2 - min2) / (max1 - min1);
    }

    // Classic Perlin noise, periodic variant
    float perlin(vec2 P) {
        vec2 rep = vec2(255.0, 255.0);

        vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
        vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
        Pi = mod(Pi, rep.xyxy); // To create noise with explicit period
        Pi = mod289(Pi);        // To avoid truncation effects in permutation
        vec4 ix = Pi.xzxz;
        vec4 iy = Pi.yyww;
        vec4 fx = Pf.xzxz;
        vec4 fy = Pf.yyww;

        vec4 i = permute(permute(ix) + iy);

        vec4 gx = fract(i * (1.0 / 41.0)) * 2.0 - 1.0;
        vec4 gy = abs(gx) - 0.5;
        vec4 tx = floor(gx + 0.5);
        gx = gx - tx;

        vec2 g00 = vec2(gx.x, gy.x);
        vec2 g10 = vec2(gx.y, gy.y);
        vec2 g01 = vec2(gx.z, gy.z);
        vec2 g11 = vec2(gx.w, gy.w);

        vec4 norm = taylorInvSqrt(vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11)));
        g00 *= norm.x;
        g01 *= norm.y;
        g10 *= norm.z;
        g11 *= norm.w;

        float n00 = dot(g00, vec2(fx.x, fy.x));
        float n10 = dot(g10, vec2(fx.y, fy.y));
        float n01 = dot(g01, vec2(fx.z, fy.z));
        float n11 = dot(g11, vec2(fx.w, fy.w));

        vec2 fade_xy = fade(Pf.xy);
        vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
        float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
        return map(2.3 * n_xy, -1.0, 1.0, 0.0, 1.0);
    }

    vec3 rgb( int c ){
            return vec3(
                float( ( c >> 16 ) & 0xff ) * 0.00392156863,
                float( ( c >> 8 ) & 0xff ) * 0.00392156863,
                float( c & 0xff ) * 0.00392156863
            );
        }


//     * @param {Object} options Options for fBm generaiton.
//    * @param {number} options.seed Seed for Perlin Noise
//    * @param {number} options.scale What distance to view the noisemap
//    * @param {number} options.persistance How much each octave contributes to the overall shape
//    * @param {number} options.lacunarity How much detail is added or removed at each octave
//    * @param {number} options.octaves Levels of detail you want you perlin noise to have
//    * @param {number} options.redistribution Level of flatness within the valleys
    float fbm(vec2 pos, float persistance, float lacunarity, float redistribution, int octaves ){
        float result    = 0.0;
        float amplitude = 1.0;
        float frequency = 1.0;
        float maximum   = amplitude;
        float noiseVal  = 0.0;
        vec2  p;

        for (int i = 0; i < octaves; i++) {
            p           = pos.xy * frequency;

            noiseVal    = perlin(p);
            result     += noiseVal * amplitude;

            frequency  *= lacunarity;
            amplitude  *= persistance;
            maximum    += amplitude;
        }

        float redistributed = pow( result, redistribution );
        return redistributed / maximum;
    }

    // #endregion
    ////////////////////////////////////////////////////////////////////////

    void main(){


    //     new FBM({
    //     seed: generation.Seed,
    //     lacunarity: generation.Detail * 4,
    //     persistance: generation.Fuzzyness * 2
    //   }),

        // getNoiseValue
        //     Math.pow(
        //         MathUtils.mapLinear( fbm.get2(vec2), -1, 1, 0, 1 ),
        //         2
        //     )

        // const scaledVector = point.clone().multiplyScalar(scale * generationScale);
        // const realHeight   = getNoiseValue(scaledVector) * generationHeight;
        // let visibleHeight  = realHeight;

        // if (display === "color" && realHeight < waterLevel) {
        //   visibleHeight = waterLevel;
        // }

        // return {
        //   x: point.x,
        //   y: point.y,
        //   z: (visibleHeight / scale) * 3,
        //   height: realHeight
        // };

        // fbm( fragUV * 2.0, vec4() );

        float Scale     = 3.0;
        float Detail    = 0.5;
        float Fuzzyness = 0.20; // 0.20

        float persistance    = Fuzzyness * 2.0;
        float lacunarity     = Detail * 4.0;
        float redistribution = 1.5;
        int   octaves        = 3; // 3

        // float n  = perlin( fragUV * Scale );
        float n = fbm( fragUV * Scale,  persistance, lacunarity, redistribution, octaves );
              n = n * 0.5 + 0.5;
              n = pow( n, 2.0 );
        
        outColor = vec4( n, n, n, 1.0 );    
        
    }`
    });
}

</script></body></html>